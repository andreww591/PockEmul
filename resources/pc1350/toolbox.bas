10 REM ** TOOL BOX **
20 REM ** Author = aldweb@multimania.com **
30 CLS : WAIT 0: CURSOR 4,1: PRINT "Please, wait..."
40 REM ** INITIALISATIONS **
50 A=32850
60 H= INT ((A+2)/256):L=A+2-H*256
70 POKE A+10,132,89,52,133,89,52
80 POKE A+16,132,02,L,219,133,02,H,219,37,218,37
90 H= INT ((A+5)/256):L=A+5-H*256
100 POKE A+27,136,00,03,53,136,16,H,L,00,03,25
110 POKE A+38,133,91,219,132,91,219,55
120 DIM B$(255)*5,B(255): FOR N=0 TO 255: READ B$(N),B(N): NEXT N
130 REM ** MENU PRINCIPAL **
140 "A": WAIT 0
150 CLS : PRINT "1.Unassembler"
160 PRINT "2.ASCII/Decimal  4.Quit"
170 PRINT "3.Byte Hunter"
180 PRINT "Input your choice : ";
190 G$= INKEY$ : IF G$="" GOTO 190
200 IF G$="1" PRINT "1": GOSUB 250: GOTO 150
210 IF G$="2" PRINT "2": GOSUB 430: GOTO 150
220 IF G$="3" PRINT "3": GOSUB 590: GOTO 150
230 IF G$="4" PRINT "4": END
240 GOTO 190
250 REM ** DESASSEMBLEUR **
260 CLS : PRINT "     UNASSEMBLER": PRINT ""
270 INPUT "Start Address : ";N: IF N<> INT N OR N<0 OR N>65535 GOTO 270
280 H= INT (N/256):L=N-H*256: POKE A,L,H
290 CALL A+10
300 K=A+5
310 M= PEEK (K):I=B(M)-1
320 IF I=1 LET P= PEEK (K+1)
330 IF I=2 LET P= PEEK (K+1)*256+ PEEK (K+2)
340 IF I=3 LET P= PEEK (K+1)*65536+ PEEK (K+2)*256+ PEEK (K+3)
350 IF M>223 PRINT USING "######";N;" CAL   "; STR$ (M-224); STR$ P: GOTO 390
360 IF M>127 AND M<192 PRINT USING "######";N;" LP    "; STR$ (M-128): GOTO 390
370 IF I=0 PRINT USING "######";N;" ";B$(M): GOTO 390
380 PRINT USING "######";N;" "; USING "&&&&&";B$(M); USING ;" "; STR$ P
390 G$= INKEY$ : IF G$="" GOTO 390
400 IF ASC (G$)=2 GOTO 260
410 IF ASC (G$)=8 RETURN
420 N=N+I+1: GOTO 280
430 REM ** ASCII/DEC **
440 CLS : PRINT "    ASCII/DECIMAL": PRINT ""
450 INPUT "Start Address : ";N: IF N<> INT N OR N<0 OR N>65535 GOTO 450
460 H= INT (N/256):L=N-H*256: POKE A,L,H
470 CALL A+10
480 M= PEEK (A+5):R= PEEK (A+6):P= PEEK (A+7):Q= PEEK (A+8)
490 I$= CHR$ M: IF M=0 LET I$=" "
500 J$= CHR$ R: IF R=0 LET J$=" "
510 K$= CHR$ P: IF P=0 LET K$=" "
520 L$= CHR$ Q: IF Q=0 LET L$=" "
530 PRINT USING "####";M;R;P;Q; USING ;" l ";I$;J$;K$;L$
540 G$= INKEY$ : IF G$="" GOTO 540
550 IF ASC (G$)=2 GOTO 440
560 IF G$="A" OR G$="a" PRINT "Addresses : "; STR$ (N+4);" to "; STR$ (N+7)
570 IF ASC (G$)=8 RETURN
580 N=N+4: GOTO 460
590 REM ** BYTE HUNTER **
600 CLS : PRINT "      BYTE HUNTER": PRINT ""
610 INPUT "Number of characters     (1 to 4) : ";I: IF I<0 OR I>4 OR I<> INT I GOTO 610
620 IF I=0 GOTO 940
630 INPUT "ASCII Code #1 : ";M: IF M<0 OR M>255 OR M<> INT M GOTO 630
640 IF I>1 INPUT "ASCII Code #2 : ";R: IF R<0 OR R>255 OR R<> INT R GOTO 640
650 IF I>2 INPUT "ASCII Code #3 : ";P: IF P<0 OR P>255 OR P<> INT P GOTO 650
660 IF I=4 INPUT "ASCII Code #4 : ";Q: IF Q<0 OR Q>255 OR Q<> INT Q GOTO 660
670 INPUT "Start Address : ";D: IF D<> INT D OR D<0 OR D>65535 GOTO 670
680 INPUT "End Address : ";F: IF F<> INT (F) OR F<0 OR F>65535 GOTO 680
690 CLS : PRINT "Search : "; STR$ M;" ";
700 IF I>1 PRINT STR$ R;" ";
710 IF I>2 PRINT STR$ P;" ";
720 IF I=4 PRINT STR$ Q;
730 PRINT ""
740 X$= STR$ D:Y$= STR$ F
750 IF LEN X$<5 LET X$=" "+X$: GOTO 750
760 IF LEN Y$<5 LET Y$=" "+Y$: GOTO 760
770 CURSOR 0,1: PRINT X$;" => ";X$;" => ";Y$
780 FOR J=D TO F: CURSOR 8,1: PRINT USING "######";J: USING
790 H= INT (J/256):L=J-H*256: POKE A,L,H
800 CALL A+10
810 K=A+5
820 IF M<> PEEK (K) GOTO 900
830 IF I>1 AND R<> PEEK (K+1) GOTO 900
840 IF I>2 AND P<> PEEK (K+2) GOTO 900
850 IF I=4 AND Q<> PEEK (K+3) GOTO 900
860 BEEP 1: CURSOR 3,3: PRINT "I found in "; STR$ J
870 G$= INKEY$ : IF G$="" GOTO 870
880 IF ASC (G$)=2 GOTO 600
890 IF ASC (G$)=8 GOTO 940
900 NEXT J
910 BEEP 1: CURSOR 3,3: PRINT "Search finished "
920 G$= INKEY$ : IF G$="" GOTO 920
930 IF ASC (G$)=2 GOTO 600
940 RETURN
950 REM ** DATA DES INSTRUCTIONS **
960 DATA "LII",2,"LIJ",2,"LIA",2,"LIB",2,"IX",1
970 DATA "DX",1,"IY",1,"DY",1,"MVW",1,"EXW",1
980 DATA "MVB",1,"EXB",1,"ADN",1,"SBN",1,"ADW",1
990 DATA "SBW",1,"LIDP",3,"LIDL",2,"LIP",2,"LIQ",2
1000 DATA "ADB",1,"SBB",1,"?022?",1,"?023?",1,"MVWD",1
1010 DATA "EXWD",1,"MVBD",1,"EXBD",1,"SRW",1,"SLW",1
1020 DATA "FILM",1,"FILD",1,"LDP",1,"LDQ",1,"LDR",1
1030 DATA "RA",1,"IXL",1,"DXL",1,"IYS",1,"DYS",1
1040 DATA "JRNZP",2,"JRNZM",2,"JRNCP",2,"JRNCM",2,"JRP",2
1050 DATA "JRM",2,"?046?",1,"LOOP",2,"STP",1,"STQ",1
1060 DATA "STR",1,"?051?",1,"PUSH",1,"DATA",1,"?054?",1
1070 DATA "RTN",1,"JRZP",2,"JRZM",2,"JRCP",2,"JRCM",2
1080 DATA "?060?",1,"?061?",1,"?062?",1,"?063?",1,"INCI",1
1090 DATA "DECI",1,"INCA",1,"DECA",1,"ADM",1,"SBM",1
1100 DATA "ANMA",1,"ORMA",1,"INCK",1,"DECK",1,"INCM",1
1110 DATA "DECM",1,"INA",1,"NOPW",1,"WAIT",2,"WAITI",1
1120 DATA "INCP",1,"DECP",1,"STD",1,"MVDM",1,"READM",1
1130 DATA "MVMD",1,"READ",1,"LDD",1,"SWP",1,"LDM",1
1140 DATA "SL",1,"POP",1,"?092?",1,"OUTA",1,"?094?",1
1150 DATA "OUTF",1,"ANIM",2,"ORIM",2,"TSIM",2,"CPIM",2
1160 DATA "ANIA",2,"ORIA",2,"TSIA",2,"CPIA",2,"?104?",1
1170 DATA "DTJ",1,"?106?",1,"TEST",2,"?108?",1,"?109?",1
1180 DATA "?110?",1,"?111?",1,"ADIM",2,"SBIM",2,"?114?",1
1190 DATA "?115?",1,"ADIA",2,"SBIA",2,"?118?",1,"?119?",1
1200 DATA "CALL",3,"JP",3,"PTJ",1,"?123?",1,"JPNZ",3
1210 DATA "JPNC",3,"JPZ",3,"JPC",3,"LP00",1,"LP01",1
1220 DATA "LP02",1,"LP03",1,"LP04",1,"LP05",1,"LP06",1
1230 DATA "LP07",1,"LP08",1,"LP09",1,"LP10",1,"LP11",1
1240 DATA "LP12",1,"LP13",1,"LP14",1,"LP15",1,"LP16",1
1250 DATA "LP17",1,"LP18",1,"LP19",1,"LP20",1,"LP21",1
1260 DATA "LP22",1,"LP23",1,"LP24",1,"LP25",1,"LP26",1
1270 DATA "LP27",1,"LP28",1,"LP29",1,"LP30",1,"LP31",1
1280 DATA "LP32",1,"LP33",1,"LP34",1,"LP35",1,"LP36",1
1290 DATA "LP37",1,"LP38",1,"LP39",1,"LP40",1,"LP41",1
1300 DATA "LP42",1,"LP43",1,"LP44",1,"LP45",1,"LP46",1
1310 DATA "LP47",1,"LP48",1,"LP49",1,"LP50",1,"LP51",1
1320 DATA "LP52",1,"LP53",1,"LP54",1,"LP55",1,"LP56",1
1330 DATA "LP57",1,"LP58",1,"LP59",1,"LP60",1,"LP61",1
1340 DATA "LP62",1,"LP63",1,"INCJ",1,"DECJ",1,"INCB",1
1350 DATA "DECB",1,"ADCM",1,"SBCM",1,"TSMA",1,"CPMA",1
1360 DATA "INCL",1,"DECL",1,"INCN",1,"DECN",1,"INB",1
1370 DATA "?205?",1,"NOPT",1,"?207?",1,"SC",1,"RC",1
1380 DATA "SR",1,"?211?",1,"ANID",2,"ORID",2,"TSID",2
1390 DATA "?215?",1,"LEAVE",1,"?217?",1,"EXAB",1,"EXAM",1
1400 DATA "?220?",1,"OUTB",1,"?222?",1,"OUTC",1,"CAL00",2
1410 DATA "CAL01",2,"CAL02",2,"CAL03",2,"CAL04",2,"CAL05",2
1420 DATA "CAL06",2,"CAL07",2,"CAL08",2,"CAL09",2,"CAL10",2
1430 DATA "CAL11",2,"CAL12",2,"CAL13",2,"CAL14",2,"CAL15",2
1440 DATA "CAL16",2,"CAL17",2,"CAL18",2,"CAL19",2,"CAL20",2
1450 DATA "CAL21",2,"CAL22",2,"CAL23",2,"CAL24",2,"CAL25",2
1460 DATA "CAL26",2,"CAL27",2,"CAL28",2,"CAL29",2,"CAL30",2
1470 DATA "CAL31",2
