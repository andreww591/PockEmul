10 REM ** TRAITEMENT DE TEXTE **
20 REM ** Author = aldweb@multimania.com **
30 CLEAR
40 A=33000:B=2000
50 CLS : WAIT 0: CURSOR 4,1: PRINT "Please, wait..."
60 DIM TT$(255)*24,LI$(0)*24,ZE$(0)*24,TR$(0)*24,MA$(0)*24
70 N=2:NM=2:M$="SML":MS=1:MI=1:PA=20:SP$="DRAWN":JU$="JUSTIFIED"
80 FOR K=1 TO 24:MA$(0)=MA$(0)+ CHR$ (249): NEXT K:TR$(0)="------------------------"
90 ZE$(0)="                        "
100 FOR K=0 TO 255:TT$(K)=ZE$(0): NEXT K
110 REM ** START PROGRAM **
120 "M": CLS : WAIT 0: PRINT "1.TEXT      4.PRINT"
130 PRINT "2.RAPID     5.SAVE"
140 PRINT "3.OPTIONS   6.LOAD MEM."
150 PRINT "Input your choice : ";
160 I$= INKEY$ : IF I$="" GOTO 160
170 IF I$="1" WAIT 120: PRINT "1": GOTO 250
180 IF I$="2" WAIT 120: PRINT "2": GOTO 1380
190 IF I$="3" WAIT 120: PRINT "3": GOTO 1530
200 IF I$="4" WAIT 120: PRINT "4": GOTO 2000
210 IF I$="5" WAIT 120: PRINT "5": GOTO 2350
220 IF I$="6" WAIT 120: PRINT "6": GOTO 2640
230 I= ASC (I$): IF I=8 CLS : END
240 GOTO 160
250 REM ** TEXT **
260 WAIT 0: GOTO 450
270 CLS : CURSOR 4,1: PRINT "Please, wait..."
280 NP=N:YP=Y:Y=2: FOR N=255 TO 0 STEP -1
290 IF TT$(N)=TR$(0) GOSUB 1240
300 IF TT$(N)=MA$(0) GOSUB 1240
310 NEXT N: IF NM<2 LET NM=2
320 IF TT$(NM)=ZE$(0) AND NM>1 LET NM=NM-1: GOTO 320
330 NM=NM+PA+MS+MI+1
340 Y=1:N=0: FOR J=1 TO MS: GOSUB 1210:TT$(N)=MA$(0): NEXT J
350 GOSUB 1210: IF SP$="DRAWN" LET TT$(N)=TR$(0)
360 FOR N=MS+PA+1 TO NM STEP PA+MS+MI+1
370 FOR J=1 TO MS: GOSUB 1210:TT$(N)=MA$(0): NEXT J
380 GOSUB 1210: IF SP$="DRAWN" LET TT$(N)=TR$(0)
390 FOR J=1 TO MI: GOSUB 1210:TT$(N)=MA$(0): NEXT J
400 IF NM>253-MI-MS LET N=NM
410 NEXT N
420 NM=NM-PA-MS-MI-1
430 N=NP:Y=YP
440 IF W=1 LET W=0: GOTO 2110
450 CLS : GOSUB 820
460 I$= INKEY$ : IF I$="" GOTO 460
470 I= ASC (I$)
480 IF Q=1 AND I=4 LET N=N-10:Q=0:R$="": GOSUB 740: GOTO 460
490 IF Q=1 AND I=5 LET N=N+10:Q=0:R$="": GOSUB 740: GOTO 460
500 IF Q=1 AND I=14 LET X=X+10:Q=0:R$="": GOSUB 740: GOTO 460
510 IF Q=1 AND I=15 LET X=X-10:Q=0:R$="": GOSUB 740: GOTO 460
520 IF I=4 LET Y=Y-1: GOSUB 740: GOTO 460
530 IF I=5 LET Y=Y+1: GOSUB 740: GOTO 460
540 IF I=14 LET X=X+1: GOSUB 740: GOTO 460
550 IF I=15 LET X=X-1: GOSUB 740: GOTO 460
560 IF I=8 GOTO 110
570 IF Q=1 AND I=11 GOSUB 1210:Q=0:R$="": GOSUB 820: GOTO 460
580 IF Q=1 AND I=12 GOSUB 1240:Q=0:R$="": GOSUB 820: GOTO 460
590 IF I=11 GOSUB 1270: GOSUB 820: GOTO 460
600 IF I=12 GOSUB 1330: GOSUB 820: GOTO 460
610 IF S=1 AND I>64 AND I<91 AND I<>77 AND I<>78 GOSUB 930:S=0:P$="": GOSUB 870: GOTO 460
620 IF S=1 AND (I=40 OR I=41 OR I=45) GOSUB 930:S=0:P$="": GOSUB 870: GOTO 460
630 IF L=0 AND I>64 AND I<91 LET I=I+32: GOSUB 870: GOTO 460
640 IF I>39 AND I<91 GOSUB 870: GOTO 460
650 IF I=32 GOSUB 870: GOTO 460
660 IF I=22 AND L=0 LET L=1:M$="CAP": GOSUB 850: GOTO 460
670 IF I=22 LET L=0:M$="SML": GOSUB 850: GOTO 460
680 IF I=18 AND Q=1 LET Q=0:R$="": GOSUB 850: GOTO 460
690 IF I=18 LET Q=1:R$="DEF ": GOSUB 850: GOTO 460
700 IF I=16 AND S=1 LET S=0:P$="": GOSUB 850: GOTO 460
710 IF I=16 LET S=1:P$="SHFT ": GOSUB 850: GOTO 460
720 IF I=2 GOTO 270
730 GOTO 460
740 REM ** GESTION DEPL. CURSEUR **
750 IF X<0 LET X=0
760 IF X>23 LET X=23
770 IF Y>2 LET Y=2:N=N+1
780 IF Y<0 LET Y=0:N=N-1
790 IF N<2 LET N=2
800 IF N>255 LET N=255
810 IF NM<N LET NM=N
820 REM ** AFFICHAGE **
830 CURSOR 0,0: FOR K=N-2 TO N: PRINT TT$(K): NEXT K
840 CURSOR X,Y: PRINT CHR$ (250)
850 CURSOR 0,3: PRINT ZE$(0);
860 CURSOR 0,3: PRINT R$;P$;M$;" L";X+1;"C";N-1+Y: RETURN
870 REM ** AFF. CARACTERE **
880 LI$(0)=TT$(N-2+Y)
890 LI$(0)= LEFT$ (LI$(0),X)+ CHR$ (I)+ RIGHT$ (LI$(0),23-X)
900 TT$(N-2+Y)=LI$(0)
910 X=X+1: IF X>23 LET X=0:Y=Y+1
920 GOSUB 740: RETURN
930 REM ** CARAC. SPECIAUX **
940 IF I=40 LET I=60: RETURN
950 IF I=41 LET I=62: RETURN
960 IF I=45 LET I=94: RETURN
970 IF I=65 LET I=96: RETURN
980 IF I=66 LET I=126: RETURN
990 IF I=67 LET I=245: RETURN
1000 IF I=68 LET I=91: RETURN
1010 IF I=69 LET I=35: RETURN
1020 IF I=70 LET I=93: RETURN
1030 IF I=71 LET I=123: RETURN
1040 IF I=72 LET I=125: RETURN
1050 IF I=73 LET I=251: RETURN
1060 IF I=74 LET I=92: RETURN
1070 IF I=75 LET I=95: RETURN
1080 IF I=76 LET I=124: RETURN
1090 IF I=79 LET I=252: RETURN
1100 IF I=80 LET I=64: RETURN
1110 IF I=81 LET I=33: RETURN
1120 IF I=82 LET I=36: RETURN
1130 IF I=83 LET I=39: RETURN
1140 IF I=84 LET I=37: RETURN
1150 IF I=85 LET I=63: RETURN
1160 IF I=86 LET I=248: RETURN
1170 IF I=87 LET I=34: RETURN
1180 IF I=88 LET I=246: RETURN
1190 IF I=89 LET I=38: RETURN
1200 IF I=90 LET I=245: RETURN
1210 REM ** INSERT LINE **
1220 FOR K=NM TO N-1+Y STEP -1:TT$(K+1)=TT$(K): NEXT K:NM=NM+1
1230 TT$(N-1+Y)=ZE$(0): RETURN
1240 REM ** DELETE LINE **
1250 FOR K=N-2+Y TO NM-1:TT$(K)=TT$(K+1): NEXT K
1260 TT$(NM)=ZE$(0):NM=NM-1: RETURN
1270 REM ** INSERT CARACTERE **
1280 LI$(0)=TT$(N-2+Y)
1290 LI$(0)= LEFT$ (LI$(0),23)
1300 LI$(0)= LEFT$ (LI$(0),X)+" "+ RIGHT$ (LI$(0),23-X)
1310 TT$(N-2+Y)=LI$(0)
1320 RETURN
1330 REM ** DELETE CARACTERE **
1340 LI$(0)=TT$(N-2+Y)
1350 LI$(0)= LEFT$ (LI$(0),X)+ RIGHT$ (LI$(0),23-X)+" "
1360 TT$(N-2+Y)=LI$(0)
1370 RETURN
1380 REM ** RAPID TEXT **
1390 CLS : WAIT 0: PRINT "    RAPID TEXT ENTRY"
1400 INPUT "First Line : ";N:N=N-1
1410 IF N<0 OR N>255 OR N<> INT (N) GOTO 1400
1420 O=N: IF N>NM LET NM=N
1430 INPUT LI$(0)
1440 IF LI$(0)=" PI " GOTO 1480
1450 TT$(N)=LI$(0):N=N+1: IF N>NM LET NM=N
1460 IF N=256 GOTO 1480
1470 GOTO 1430
1480 CLS : CURSOR 4,1: PRINT "Please, wait..."
1490 FOR K=O TO N
1500 IF LEN TT$(K)<24 LET TT$(K)=TT$(K)+" ": GOTO 1500
1510 NEXT K: IF N<2 LET N=2
1520 GOTO 110
1530 REM ** OPTIONS **
1540 CLS : WAIT 0: PRINT "1.TOP.MARG   4.PAGE.CUT"
1550 PRINT "2.DOWN.MARG  5.JUSTIFY"
1560 PRINT "3.PAGE.LEN"
1570 PRINT "Input your choice : ";
1580 I$= INKEY$ : IF I$="" GOTO 1580
1590 IF I$="1" WAIT 120: PRINT "1": GOTO 1660
1600 IF I$="2" WAIT 120: PRINT "2": GOTO 1720
1610 IF I$="3" WAIT 120: PRINT "3": GOTO 1780
1620 IF I$="4" WAIT 120: PRINT "4": GOTO 1840
1630 IF I$="5" WAIT 120: PRINT "5": GOTO 1920
1640 I= ASC (I$): IF I=8 GOTO 110
1650 GOTO 1580
1660 REM ** MARGE SUPERIEURE **
1670 CLS : WAIT 0: PRINT "      TOP MARGIN"
1680 PRINT "Current = ";MS
1690 INPUT "New : ";MS
1700 IF MS<1 OR MS<> INT (MS) OR MS>50 GOTO 1690
1710 GOTO 1530
1720 REM ** MARGE INFERIEURE **
1730 CLS : WAIT 0: PRINT "      DOWN MARGIN"
1740 PRINT "Current = ";MI
1750 INPUT "New : ";MI
1760 IF MI<1 OR MI<> INT (MI) OR MI>50 GOTO 1750
1770 GOTO 1530
1780 REM ** PAGINE **
1790 CLS : WAIT 0: PRINT "      PAGE LENGTH"
1800 PRINT "Current = ";PA
1810 INPUT "New : ";PA
1820 IF PA<1 OR PA<> INT (PA) OR PA>255 GOTO 1810
1830 GOTO 1530
1840 REM ** SEPARATION VISIBLE DES PAGES **
1850 CLS : WAIT 0: PRINT "     CUT OF PAGES"
1860 PRINT "Current = ";SP$
1870 PRINT "Draw cut (Y/N) : ";
1880 I$= INKEY$ : IF I$="" GOTO 1880
1890 IF I$="Y" OR I$="y" WAIT 120: PRINT "Y":SP$="DRAWN": GOTO 1530
1900 IF I$="N" OR I$="n" WAIT 120: PRINT "N":SP$="NOT DR.": GOTO 1530
1910 GOTO 1880
1920 REM ** JUSTIFICATION **
1930 CLS : WAIT 0: PRINT " RIGHT JUSTIFY OF TEXT"
1940 PRINT "Current = ";JU$
1950 PRINT "Justify (Y/N) : ";
1960 I$= INKEY$ : IF I$="" GOTO 1960
1970 IF I$="Y" OR I$="y" WAIT 120: PRINT "Y":JU$="JUSTIFIED": GOTO 1530
1980 IF I$="N" OR I$="n" WAIT 120: PRINT "N":JU$="NOT JUST.": GOTO 1530
1990 GOTO 1960
2000 REM ** PRINT **
2010 CLS : WAIT 0
2020 PRINT "TM ="; USING "###";MS;"  PAGE.LEN ="; USING "####";PA
2030 PRINT "DM ="; USING "###";MI;"  Cut ";SP$: USING
2040 PRINT "Right ";JU$;" text"
2050 PRINT "Print text (Y/N) ? ";
2060 I$= INKEY$ : IF I$="" GOTO 2060
2070 IF I$="Y" OR I$="y" WAIT 120: PRINT "Y": GOTO 2100
2080 IF I$="N" OR I$="n" WAIT 120: PRINT "N": GOTO 110
2090 GOTO 2060
2100 WAIT 0:W=1: GOTO 270
2110 CLS : CURSOR 5,1: PRINT "Press any key": PRINT "    to stop printing"
2120 FOR K=0 TO NM+PA+MS+MI+1
2130 I$= INKEY$ : IF I$<>"" LET K=NM+PA+MS+MI+1: GOTO 2180
2140 IF TT$(K)=MA$(0) LPRINT ZE$(0): GOTO 2180
2150 IF TT$(K)=ZE$(0) LPRINT ZE$(0): GOTO 2180
2160 IF JU$="JUSTIFIED" GOSUB 2200
2170 LPRINT LI$(0)
2180 NEXT K
2190 GOTO 110
2200 REM ** JUSTIFICATION
2210 LI$(0)=TT$(K)
2220 IF RIGHT$ (LI$(0),1)=" " LET LI$(0)= LEFT$ (LI$(0), LEN LI$(0)-1): GOTO 2220
2230 IF LEN LI$(0)=24 GOTO 2340
2240 IF RIGHT$ (LI$(0),1)="@" LET LI$(0)= LEFT$ (LI$(0), LEN LI$(0)-1): GOTO 2340
2250 F=0: FOR J=1 TO LEN LI$(0): IF MID$ (LI$(0),J,1)=" " LET F=1
2260 NEXT J
2270 IF F=1 LET H=1: GOTO 2290
2280 GOTO 2340
2290 FOR J=1 TO 24
2300 IF MID$ (LI$(0),J,1)=" " LET LI$(0)= LEFT$ (LI$(0),J)+" "+ RIGHT$ (LI$(0), LEN LI$(0)-J):J=J+H
2310 IF LEN LI$(0)=24 LET J=24
2320 NEXT J
2330 IF LEN LI$(0)<24 LET H=H+1: GOTO 2290
2340 RETURN
2350 REM ** SAVE **
2360 CLS : WAIT 0:C=10+24*(NM+PA+MS+MI+2)
2370 IF C<B GOTO 2420
2380 PRINT "Not enough memory space to save..."
2390 PRINT "Press any key to go backto main menu."
2400 I$= INKEY$ : IF I$="" GOTO 2400
2410 GOTO 110
2420 PRINT "1.SAVE MEM."
2430 PRINT "2.SAVE TAPE"
2440 CURSOR 0,3: PRINT "Input your choice : ";
2450 I$= INKEY$ : IF I$="" GOTO 2450
2460 IF I$="1" WAIT 120: PRINT "1": GOTO 2500
2470 IF I$="2" WAIT 120: PRINT "2": GOTO 2500
2480 I= ASC (I$): IF I=8 GOTO 110
2490 GOTO 2450
2500 CLS : WAIT 0: CURSOR 4,1: PRINT "Please, wait..."
2510 T=A
2520 IF SP$="DRAWN" LET SP=1
2530 IF JU$="JUSTIFIED" LET JU=1
2540 POKE T,NM,N,X,Y,MS,MI,PA,SP,JU,0:T=T+10
2550 FOR K=0 TO NM+PA+MS+MI+1
2560 FOR J=1 TO 24
2570 U= ASC ( MID$ (TT$(K),J,1)): POKE T+24*K+J,U
2580 NEXT J: NEXT K
2590 IF I$="1" GOTO 2350
2600 CLS : CURSOR 5,1: PRINT "Press any key": PRINT "    to start saving"
2610 I$= INKEY$ : IF I$="" GOTO 2610
2620 CSAVE M"TT";T-10,T+B
2630 GOTO 2350
2640 REM ** LOAD **
2650 CLS : WAIT 0: CURSOR 4,1: PRINT "Please, wait..."
2660 T=A
2670 NM= PEEK (T):N= PEEK (T+1):X= PEEK (T+2):Y= PEEK (T+3):MS= PEEK (T+4):MI= PEEK (T+5):PA= PEEK (T+6)
2680 SP= PEEK (T+7):JU= PEEK (T+8)
2690 IF SP=0 LET SP$="NOT DR.": GOTO 2710
2700 SP$="DRAWN"
2710 IF JU=0 LET JU$="NOT JUST.": GOTO 2730
2720 JU$="JUSTIFIED"
2730 T=T+10: FOR K=0 TO NM+PA+MS+MI+1:TT$(K)="": FOR J=1 TO 24
2740 U= PEEK (T+K*24+J):TT$(K)=TT$(K)+ CHR$ (U)
2750 NEXT J: NEXT K
2760 GOTO 110
