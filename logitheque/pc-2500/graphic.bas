
10 DEGREE:CLEAR : CONSOLE 39: LPRINT : CLS
20 LPRINT CHR$ 27;"a";chr$13:LPRINT CHR$ (27);"b":LPRINT "L0"
30 CL=-1:WAIT 0
40 CURSOR 0,0:PRINT "Angle ="
50 CURSOR 0,1:PRINT "Size  ="
60 CURSOR 0,2:PRINT "Reduct. Rate(1or0.9)"
70 CURSOR 0,3:PRINT "Draw in & around(Y/N)"
80 CURSOR 8,0:INPUT K: IF K<=2 CLS:GOTO 40
90 TH=360/K
100 CURSOR 8,1:INPUT S
110 CURSOR 20,2:INPUT RT
120 IF RT=1 LET JJ=10
130 IF RT=.9 LET JJ=6
140 IF RT<>1 AND RT<>.9 GOTO 110
150 CURSOR 22,3:INPUT Y$
160 IF Y$="Y" OR Y$="y" GOSUB 620:GOTO 300
170 IF Y$="N" OR Y$="n" GOTO 190
180 GOTO 150
190 SX=240:SY=-S
200 CLS:CURSOR 0,0:PRINT "Select color"
210 CURSOR 0,2:PRINT "Black==>0"
220 CURSOR 0,3:PRINT "Blue ==>1"
230 CURSOR 11,2:PRINT "Green==>2"
240 CURSOR 11,3:PRINT "Red  ==>3"
250 CURSOR 12,0:INPUT CL
260 IF (CL=0)+(CL=1)+(CL=2)+(CL=3)<>1 GOTO 200
270 CL=CL-1
280 CLS:CURSOR 0,0:PRINT "** Computer Graphics **"
290 CURSOR 7,1: PRINT "Printout": GOSUB 330
300 LPRINT "R";-KX;",";-KY-3*S:LPRINT "A"
310 LPRINT CHR$ 27;"0":CLS
320 END
330 REM   * Write *
340 LPRINT "M";SX;",";SY:LPRINT "I"
350 R=S:KX=0:KY=S
360 IF L=K AND CL=3 LET CL=1
370 IF CL=3 LET CL=0
380 CL=CL+1
390 FOR J=1 TO JJ
400 IF J=1 LET X1=0:Y1=R: GOTO 440
410 R=R*RT
420 X1=INT (R* SIN ((J-1)*(TH/10)))
430 Y1=INT (R* COS ((J-1)*(TH/10)))
440 LPRINT "M";X1;",";Y1
450 FOR I=1 TO K
460 KX=INT (R* SIN (I*TH+(J-1)*(TH/10)))
470 KY=INT (R* COS (I*TH+(J-1)*(TH/10)))
480 LPRINT CHR$(27);CL
490 LPRINT "D";KX;",";KY
500 X1=KX:Y1=KY
510 NEXT I
520 NEXT J
530 RETURN
540 REM * Rotation *
550 FOR L=1 TO K
560 SX=INT (S*1.5*SIN(L*TH))
570 SY=INT (S*1.5*COS(L*TH))
580 GOSUB 390
590 LPRINT "M";-SX;",";-SY:LPRINT "I"
600 NEXT L
610 RETURN
620 CLS : CURSOR 0,0: PRINT "** Computer Graphics **"
630 CURSOR 7,1:PRINT "Printout"
640 SX=240:SY=-3*S:GOSUB 330:GOSUB 540
650 RETURN
